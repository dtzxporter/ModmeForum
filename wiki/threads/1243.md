# Detect damage on a model ( NO TRIGGERS )
Game Modding | Call of Duty: Black Ops 3 | Scripting

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Thread By: Harry Bo21<br />{% highlight php %}{% raw %}
function your_function_name()
{
	e_model = getEnt( "YOUR_KVP_VALUE", "script_noteworthy" );
	e_model setCanDamage( 1 );
    
	while ( 1 )
	{
		e_model waittill( "damage", damage, attacker, dir, point, mod, model, tag, part, weapon, flags, inflictor, chargeLevel );
        
		if ( weapon.name == "weapon_name" ) // Optional if you want a certain weapon only to count
			break;
        
	}
	
	// Your code to run when this model is damaged
	playFx( "fx_path\fx_name", e_model.origin ); // If you want a fx to play on the model when it is damaged
	e_model delete(); // If you want the model to delete when shot
}
{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: mathfag<br />Don&#39;t forget to precache</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Harry Bo21<br /><blockquote><em>mathfag</em><br />Don&#39;t forget to precache</blockquote><br /> ...<br /> <br />everything in level._effect is precached already<br /> <br />precaching again wastes one of the available 800 fx</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: mathfag<br />You wrote <span style="color:#00ff00;">&quot;fx_path\fx_name&quot;</span> not level._effect[&quot;<span style="color:#00ff00;">fx_path\fx_name</span>&quot;] therefore precache. :)</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Abnormal202<br /><blockquote><em>mathfag</em><br />You wrote &quot;fx_path\fx_name&quot; not level._effect[&quot;fx_path\fx_name&quot;] therefore precache. :)</blockquote><br /> oh c&#39;mon you&#39;re just being nitpicky on purpose. people should know how FX works, and if not they should know this script is not the place to learn it, as FX isn&#39;t even related to the topic at hand.</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: Harry Bo21<br />Detect damage on a model ( NO TRIGGERS )<br /> <br />Detect damage on a model<br /> <br />not <br /> <br />how to play a fx when a model is shot<br /> <br />i merely advised where to play a fx and how to delete the model - as OPTIONAL extra snippits of info, and the way to do it, not a full tutorial on playing fx<br /> <br />at least my code WORKS so do one</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br />Nice script, by the way, how i can change the &quot;e_model&quot; is a &quot;zombie&quot; and only count the &quot;melee weapons&quot;?<br />How i can detect the player is knifing a zombie?<br />{% highlight php %}{% raw %}
function your_function_name()
{
	zombies = level.zombie_team;
    
	while ( 1 )
	{
		zombies waittill( "damage", damage, attacker, dir, point, mod, model, tag, part, weapon, flags, inflictor, chargeLevel );
        
		if ( weapon.name == "MOD_MELEE" ) // Optional if you want a certain weapon only to count
			break;
		}        
	}

	//my code	
	
}

{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: ihmiskeho<br /><blockquote><em>eDeK</em><br />Nice script, by the way, how i can change the &quot;e_model&quot; is a &quot;zombie&quot; and only count the &quot;melee weapons&quot;? How i can detect the player is knifing a zombie? function your_function_name() { zombies = level.zombie_team; while ( 1 ) { zombies waittill( &quot;damage&quot;, damage, attacker, dir, point, mod, model, tag, part, weapon, flags, inflictor, chargeLevel ); if ( weapon.name == &quot;MOD_MELEE&quot; ) // Optional if you want a certain weapon only to count break; } } //my code }</blockquote><br /> <br />{% highlight php %}{% raw %}
if ( mod == "MOD_MELEE" )
{% endraw %}{% endhighlight %}
Also you should check that the damager is actually a player<br />{% highlight php %}{% raw %}
if ( mod == "MOD_MELEE" &amp;&amp; IsPlayer(attacker) )
{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br />Im doing a Powerup, this function is &quot;when the player knife the zombie&quot;, but not work, i dont know how detect &quot;the player is knifing a zombie&quot;.<br />{% highlight php %}{% raw %}
function knife_me()
{
	self endon( "disconnect" );
    self endon( "death" );
    self endon( "pyrotechnic_done" );

    zombies = level.zombie_team;
               
    zombies waittill( "damage", damage, attacker, dir, point, mod, model, tag, part, weapon, flags, inflictor, chargeLevel );      
    {
	    if ( mod == "MOD_MELEE" &amp;&amp; IsPlayer(attacker) )	 
	    {
	        self fire_works_summon( self );	
	    }
	}    

	WAIT_SERVER_FRAME;	
    
}
{% endraw %}{% endhighlight %}
Thanks for reply/help.</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: tom5300<br /><blockquote><em>eDeK</em><br />Im doing a Powerup, this function is &quot;when the player knife the zombie&quot;, but not work, i dont know how detect &quot;the player is knifing a zombie&quot;. function knife_me() { self endon( &quot;disconnect&quot; ); self endon( &quot;death&quot; ); self endon( &quot;pyrotechnic_done&quot; ); zombies = level.zombie_team; zombies waittill( &quot;damage&quot;, damage, attacker, dir, point, mod, model, tag, part, weapon, flags, inflictor, chargeLevel ); { if ( mod == &quot;MOD_MELEE&quot; &amp;&amp; IsPlayer(attacker) ) { self fire_works_summon( self ); } } WAIT_SERVER_FRAME; } Thanks for reply/help.</blockquote><br /> You cannot waittill on a variable containing the string &quot;axis&quot;. You will have to iterate through each zombie, and thread a waittill on each zombie.<br /> <br />I&#39;d try something like:<br /> <br />{% highlight php %}{% raw %}
function knife_me()
{
    zombies = GetAiTeamArray( level.zombie_team );
    foreach(zombie in zombies)
    {
        zombie thread wait_for_knife();
    }
}
function wait_for_knife()
{
    self endon( "disconnect" );
    self endon( "death" );
    self endon( "pyrotechnic_done" );
    while(1)
    {
        self waittill( "damage", damage, attacker, dir, point, mod, model, tag, part, weapon, flags, inflictor, chargeLevel );
        if ( mod == "MOD_MELEE" &amp;&amp; IsPlayer(attacker) )	 
        {
            self fire_works_summon( self );	
        }
        WAIT_SERVER_FRAME;
    }
}
{% endraw %}{% endhighlight %}
 <br />Note: I have not tested this code, hope it&#39;s error free.</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br />Thanks for reply Tom but dont work it.<br />I need like <a href="https://aviacreations.com/modme/index.php?view=forumprofile&uid=381">&lt;span style=&quot;color:#ffffff;&quot;&gt;ihmiskeho&lt;/span&gt;</a> say &quot;the damager is actually a player&quot;.<br />Is something like that:<br />{% highlight php %}{% raw %}
if(isdefined(attacker) &amp;&amp; attacker.team != self.team &amp;&amp; !isdefined(attacker.playername))  
{
	if ( mod == "MOD_MELEE" &amp;&amp; IsPlayer(attacker) )	 
    {
        self fire_works_summon( self );	
    }
}
{% endraw %}{% endhighlight %}
But this no work too. :(</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: mathfag<br />{% highlight php %}{% raw %}
zm_spawner::register_zombie_damage_callback( &amp;knife_dmg ); //in function main

//when a AI is damaged, this will run
//self = AI
function knife_dmg(str_mod, str_hit_location, v_hit_origin, e_player, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel) 
{

if(str_mod == "MOD_MELEE")
	self thread fire_works_summon();

}

function fire_works_summon()
{
//use this new function or you may run into problems


}
{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br />Works fine Mathfag, thanks for reply dude, the problem is when i grab the powerup the &quot;Knifing effect (fireworks)&quot; dont stop, still active after the powerup is over.<br />{% highlight php %}{% raw %}
function __init__()
{
	
	zm_powerups::register_powerup( "pyrotechnic", &amp;grab_pyrotechnic );
	if( ToLower( GetDvarString( "g_gametype" ) ) != "pyrotechnic" )
	{
		zm_powerups::add_zombie_powerup( "pyrotechnic", "pyrotechnic_powerup_model", "", &amp;zm_powerups::func_should_always_drop, POWERUP_ONLY_AFFECTS_GRABBER, !POWERUP_ANY_TEAM, !POWERUP_ZOMBIE_GRABBABLE );						
	} 
}

function grab_pyrotechnic( player )
{
	player PlayLocalSound("vox_pyrotechnic");
	skip = player add_powerup_hud( "pyrotechnic_material_hud", N_POWERUP_DEFAULT_TIME );
    zm_spawner::register_zombie_damage_callback( &amp;knife_dmg );    

	if( skip )
		return; 
	
	if( isdefined(player.pyrotechnic_active) )
		return;		
	
	player thread do_pyrotechnic();
}

function do_pyrotechnic()
{
	self.pyrotechnic_active = true;

    self notify( "pyrotechnic_done" );
    self endon( "disconnect" );
    self endon( "death" );
    self endon( "pyrotechnic_done" );

    self thread knife_dmg();	
}

function knife_dmg(str_mod, str_hit_location, v_hit_origin, e_player, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel) 
{		
    self endon( "disconnect" );
    self endon( "death" );
    self endon( "pyrotechnic_done" );

	if(str_mod == "MOD_MELEE")
	{
		self thread fire_works_summon( e_player, w_weapon );
	}

	WAIT_SERVER_FRAME;
}

{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: mathfag<br />Put this in __init__. It should only be activated once.<br />{% highlight php %}{% raw %}
zm_spawner::register_zombie_damage_callback( &amp;knife_dmg );
{% endraw %}{% endhighlight %}
 <br />Remove<br />{% highlight php %}{% raw %}
self thread knife_dmg();
{% endraw %}{% endhighlight %}
 <br />Replace<br />{% highlight php %}{% raw %}
self.pyrotechnic_active = true;
{% endraw %}{% endhighlight %}
with<br />{% highlight php %}{% raw %}
level.pyrotechnic_active = true;
{% endraw %}{% endhighlight %}
 <br />Replace function knife_dmg() with<br />{% highlight php %}{% raw %}
function knife_dmg(str_mod, str_hit_location, v_hit_origin, e_player, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel) 
{		

if(level.pyrotechnic_active == 0)
return 0;

	if(str_mod == "MOD_MELEE")
	{
		self thread fire_works_summon( e_player, w_weapon );
	}

	WAIT_SERVER_FRAME;
}
{% endraw %}{% endhighlight %}
 <br />Make sure<br />{% highlight php %}{% raw %}
level.pyrotechnic_active
{% endraw %}{% endhighlight %}
is set to false when the powerup is over</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br />I do all you say. <br />When i spawn in the game (I shoot the zombies, i cant do points and the &quot;Knife effect is active&quot;).<br />After this, i grab the powerup (I shoot the zombies, i can do points but the &quot;Knife effect is no active&quot; even after powerup is over.&quot;).<br />Very weird all, i gonna still testing but at the moment i have this.<br />{% highlight php %}{% raw %}
//POWERUP
function __init__()
{	
	zm_powerups::register_powerup( "pyrotechnic", &amp;grab_pyrotechnic );
	if( ToLower( GetDvarString( "g_gametype" ) ) != "pyrotechnic" )
	{
		zm_powerups::add_zombie_powerup( "pyrotechnic", "pyrotechnic_powerup_model", "", &amp;zm_powerups::func_should_always_drop, POWERUP_ONLY_AFFECTS_GRABBER, !POWERUP_ANY_TEAM, !POWERUP_ZOMBIE_GRABBABLE );
		zm_spawner::register_zombie_damage_callback( &amp;knife_dmg );					
	} 
}

function grab_pyrotechnic( player )
{
	player PlayLocalSound("vox_pyrotechnic");
	skip = player add_powerup_hud( "pyrotechnic_material_hud", N_POWERUP_DEFAULT_TIME );    	

	if( skip )
		return; 
	
	if( isdefined(player.pyrotechnic_active) )
		return;		
	
	player thread do_pyrotechnic();
}

function do_pyrotechnic()
{
	level.pyrotechnic_active = true;

    self notify( "pyrotechnic_done" );
    self endon( "disconnect" );
    self endon( "death" );
    self endon( "pyrotechnic_done" );    
}

function knife_dmg(str_mod, str_hit_location, v_hit_origin, e_player, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel) 
{		

	if(level.pyrotechnic_active == 0)
	return 0;

	if(str_mod == "MOD_MELEE")
	{
		self thread fire_works_summon( e_player, w_weapon );
	}

	WAIT_SERVER_FRAME;
}

//HUD
function wait_til_timeout( player, hud )//player = self;
{
	while( hud.time &gt; 0 )
	{
		wait(1);
		hud.time--; 		
	}
	
	player notify( "pyrotechnic_done" );
	player remove_powerup_hud( "pyrotechnic_material_hud" );
	player.pyrotechnic_active = undefined;
	
	
}

function add_powerup_hud( powerup, timer )
{
	if ( !isDefined( self.powerup_hud ) )
		self.powerup_hud = [];
	
	if( isDefined( self.powerup_hud[powerup] ) )
	{
		self.powerup_hud[powerup].time = timer; 
		return true; // tells to skip because powerup is already active
				
	}
	
	self endon( "disconnect" );
	hud = NewClientHudElem( self );
	hud.powerup = powerup;
	hud.foreground = true;
	hud.hidewheninmenu = true;
	hud.alignX = "center";
	hud.alignY = "bottom";
	hud.horzAlign = "center";
	hud.vertAlign = "bottom";
	hud.x = hud.x;
	hud.y = hud.y - 50;
	hud.alpha = 1;
	hud SetShader( powerup , 64, 64 );
	hud scaleOverTime( .5, 32, 32 );
	hud.time = timer;
	hud thread harrybo21_blink_powerup_hud();
	thread wait_til_timeout( self, hud ); 
	
	self.powerup_hud[ powerup ] = hud;
	
	a_keys = GetArrayKeys( self.powerup_hud );
	for ( i = 0; i &lt; a_keys.size; i++ )
	 	self.powerup_hud[ a_keys[i] ] thread move_hud( .5, 0 - ( 24 * ( self.powerup_hud.size ) ) + ( i * 37.5 ) + 25, self.powerup_hud[ a_keys[i] ].y );
	
	//return false;  // powerup is not already active
	level.pyrotechnic_active = false;
		

}

function move_hud( time, x, y )
{
	self moveOverTime( time );
	self.x = x;
	self.y = y;
}

function harrybo21_blink_powerup_hud()
{
	self endon( "delete" );
	self endon( "stop_fade" );
	while( isDefined( self ) )
	{
		if ( self.time &gt;= 20 )
		{
			self.alpha = 1; 
			wait .1;
			continue;
		}
		fade_time = 1;
		if ( self.time &lt; 10 )
			fade_time = .5;
		if ( self.time &lt; 5 )
			fade_time = .25;
			
		self fadeOverTime( fade_time );
		self.alpha = !self.alpha;
		
		wait( fade_time );
	}
}

function remove_powerup_hud( powerup )
{
	self.powerup_hud[ powerup ] destroy();
	self.powerup_hud[ powerup ] notify( "stop_fade" );
	self.powerup_hud[ powerup ] fadeOverTime( .2 );
	self.alpha = 0;
	wait .2;
	self.powerup_hud[ powerup ] delete();
	self.powerup_hud[ powerup ] = undefined;
	self.powerup_hud = array::remove_index( self.powerup_hud, self.powerup_hud[ powerup ], true );
	
	a_keys = GetArrayKeys( self.powerup_hud );
	for ( i = 0; i &lt; a_keys.size; i++ )
	 	self.powerup_hud[ a_keys[i] ] thread move_hud( .5, 0 - ( 24 * ( self.powerup_hud.size ) ) + ( i * 37.5 ) + 25, self.powerup_hud[ a_keys[i] ].y );
}


//FIREWORKS
// Checks to see if fire works is running
// self == zombie
function fire_works_zombie_validation()
{
	if( IS_TRUE( self.barricade_enter ) )
	{
		return false;
	}
	
	if ( IS_TRUE( self.is_traversing ) )
	{
		return false;
	}

	if( !IS_TRUE( self.completed_emerging_into_playable_area ) &amp;&amp; !IsDefined( self.first_node ) )
	{
		return false;
	}

	if ( IS_TRUE( self.is_leaping ) )
	{
		return false;
	}
	
	return true;
}

// Summons the player&#39;s current gun to pop up and fire in a circle for a period of time
// immune_result_direct == target is immune to death gib
// immune_result_indirect == target is immune to death gib on hit from the magic bullet
// self == target zombie
function fire_works_summon( e_player, w_weapon )
{
	w_summoned_weapon = e_player GetCurrentWeapon();
	v_target_zombie_origin = self.origin;
	
	// Checks if self is immune_result_direct == true. If so, do not kill self
	if ( !IS_TRUE( level.aat[ ZM_AAT_FIRE_WORKS_NAME ].immune_result_direct[ self.archetype ] ) )
	{
		self thread zombie_death_gib( e_player, w_weapon, e_player );
	}

	// Spawns base model
	v_firing_pos = v_target_zombie_origin + ZM_AAT_FIRE_WORKS_ZOMBIE_GUN_HEIGHT;
	v_start_yaw = VectorToAngles( v_firing_pos - v_target_zombie_origin );
	v_start_yaw = (0, v_start_yaw[1], 0);
	mdl_weapon = zm_utility::spawn_weapon_model( w_summoned_weapon, undefined, v_target_zombie_origin, v_start_yaw );

	// Stat tracking definitions
	mdl_weapon.owner = e_player;
	mdl_weapon.b_aat_fire_works_weapon = true;
	mdl_weapon.allow_zombie_to_target_ai = true; // lets the zombie damage callbacks pass through damage from this
	
	// Fires FX
	mdl_weapon thread clientfield::set( ZM_AAT_FIRE_WORKS_NAME, 1 );
	
	// Moves weapon upwards to firing position
	mdl_weapon MoveTo( v_firing_pos, ZM_AAT_FIRE_WORKS_SUMMON_TIME );
	mdl_weapon waittill( "movedone" );

	// Starts firing
	for ( i = 0; i &lt; ZM_AAT_FIRE_WORKS_FIRING_NUM_FRAMES; i++ )
	{
		zombie = mdl_weapon zm_aat_fire_works_get_target();
		if ( !IsDefined( zombie ) )
		{
			//if no target available, just pick a random yaw
			v_curr_yaw = (0, RandomIntRange( 0, 360 ), 0);
			v_target_pos = mdl_weapon.origin + VectorScale( AnglesToForward( v_curr_yaw ), 40 );
		}
		else
		{
			v_target_pos = zombie GetCentroid();
		}

		mdl_weapon.angles = VectorToAngles( v_target_pos - mdl_weapon.origin );
		v_flash_pos = mdl_weapon GetTagOrigin( "tag_flash" );
		mdl_weapon DontInterpolate();

		// MagicBullet shots are credited to the model rather than player, as MagicBullet causes recoil on player
		MagicBullet( w_summoned_weapon, v_flash_pos, v_target_pos, mdl_weapon );

		util::wait_network_frame();
	}

	mdl_weapon MoveTo( v_target_zombie_origin, ZM_AAT_FIRE_WORKS_SUMMON_TIME );
	mdl_weapon waittill( "movedone" );
	
	mdl_weapon clientfield::set( ZM_AAT_FIRE_WORKS_NAME, 0 );
	
	util::wait_network_frame(); // Waits for FX to complete
	util::wait_network_frame(); // extra waits for theater playback
	util::wait_network_frame(); // extra waits for theater playback
	
	mdl_weapon Delete();
	wait .25; // Delay for final projectile-based gun shots to finish firing
}

// Death callback for zombies killed by summoned Fire Works weapon
function zm_aat_fire_works_get_target()
{
	a_ai_zombies = array::randomize( GetAiTeamArray( "axis" ) );

	los_checks = 0;
	for ( i = 0; i &lt; a_ai_zombies.size; i++ )
	{
		zombie = a_ai_zombies[i];
		test_origin = zombie getcentroid();
		if ( DistanceSquared( self.origin, test_origin ) &gt; ZM_AAT_FIRE_WORKS_RANGE_SQ )
		{
			continue;
		}

		if ( los_checks &lt; ZM_AAT_FIRE_WORKS_MAX_LOS_CHECKS &amp;&amp; !zombie DamageConeTrace( self.origin ) )
		{
			los_checks++;
			continue;
		}

		return zombie;
	}

	if ( a_ai_zombies.size )
	{
		// just return the first one, so that we at least change direction
		return a_ai_zombies[0];
	}

	return undefined;
}

// self is a zombie
function zm_aat_fire_works_zombie_damage_response( str_mod, str_hit_location, v_hit_origin, e_attacker, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel )
{
	if ( IS_TRUE( level.aat[ ZM_AAT_FIRE_WORKS_NAME ].immune_result_indirect[ self.archetype ] ) )
	{
		return false;
	}

	if ( IS_TRUE( e_attacker.b_aat_fire_works_weapon ) )
	{
		self thread zombie_death_gib( e_attacker, w_weapon, e_attacker.owner );
		return true;
	}

	return false;
}

// Death callback for zombies killed by summoned Fire Works weapon
function zm_aat_fire_works_death_callback( attacker )
{
	if ( isdefined( attacker ) )
	{
		if ( IS_TRUE( attacker.b_aat_fire_works_weapon ) )
		{
			// Checks if player has disconnected
			if ( isdefined( attacker.owner ) )
			{
				e_attacking_player = attacker.owner;
				// TODO set up stat tracking
			}
		}
	}
}

// Gibs and Kills zombie
// self == affected zombie
// e_attacker == the script_model of the gun (needs to do the damage, so the player doesn&#39;t receive kickback)
// w_weapon == the weapon to apply damage using
// e_owner == the owner of the gun (for awarding challenge stat progress)
function zombie_death_gib( e_attacker, w_weapon, e_owner )
{
	gibserverutils::gibhead( self );
	
	if ( math::cointoss() )
	{
		gibserverutils::gibleftarm( self );
	}
	else
	{
		gibserverutils::gibrightarm( self );
	}
	
	gibserverutils::giblegs( self );
	
	self DoDamage( self.health, self.origin, e_attacker, w_weapon, "torso_upper" );

	if ( IsDefined( e_owner ) &amp;&amp; IsPlayer( e_owner ) )
	{
		e_owner zm_stats::increment_challenge_stat( "ZOMBIE_HUNTER_FIRE_WORKS" );
	}
}

{% endraw %}{% endhighlight %}
Thanks for your time Mathfag.</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: mathfag<br />in grab_pyrotechnic change <br />{% highlight php %}{% raw %}
if( isdefined(player.pyrotechnic_active) )
{% endraw %}{% endhighlight %}
 <br />to<br />{% highlight php %}{% raw %}
if( isdefined(player.pyrotechnic_active) &amp;&amp; player.pyrotechnic_active == 1 )
{% endraw %}{% endhighlight %}
 <br />just incase.<br /> <br /> <br />The problem is that you set <br />{% highlight php %}{% raw %}
level.pyrotechnic_active = false;
{% endraw %}{% endhighlight %}
 <br />immediately after setting it to true. Actually you did it before but im guessing since the cpu has to process a bunch of code it gets set after.<br />anyway<br />Move<br />{% highlight php %}{% raw %}
level.pyrotechnic_active = false;
{% endraw %}{% endhighlight %}
 <br />to the end of <br />{% highlight php %}{% raw %}
function wait_til_timeout
{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br /><blockquote><em>mathfag</em><br />in grab_pyrotechnic change  if( isdefined(player.pyrotechnic_active) )   to if( isdefined(player.pyrotechnic_active) &amp;&amp; player.pyrotechnic_active == 1 )   just incase.     The problem is that you set  level.pyrotechnic_active = false;   immediately after setting it to true. Actually you did it before but im guessing since the cpu has to process a bunch of code it gets set after. anyway Move level.pyrotechnic_active = false;   to the end of  function wait_til_timeout    </blockquote><br /> I dont have words to describe my gratitude with you, seriously, thanks.<br /><br /> <br />When i spawn in the game the &quot;Knife effect&quot; is active, i grab the powerup for the first time, the &quot;Knife effect&quot; still active, when the powerup is over, the &quot;Knife effect&quot; is over. (Everything goes back to normal)<br /> <br />I grab the powerup for the second time, the powerup is working perfect all the time.<br />{% highlight php %}{% raw %}
//POWERUP
function __init__()
{	
	zm_powerups::register_powerup( "pyrotechnic", &amp;grab_pyrotechnic );
	if( ToLower( GetDvarString( "g_gametype" ) ) != "pyrotechnic" )
	{
		zm_powerups::add_zombie_powerup( "pyrotechnic", "pyrotechnic_powerup_model", "", &amp;zm_powerups::func_should_always_drop, POWERUP_ONLY_AFFECTS_GRABBER, !POWERUP_ANY_TEAM, !POWERUP_ZOMBIE_GRABBABLE );
		zm_spawner::register_zombie_damage_callback( &amp;knife_dmg );					
	} 
}

function grab_pyrotechnic( player )
{
	player PlayLocalSound("vox_pyrotechnic");
	skip = player add_powerup_hud( "pyrotechnic_material_hud", N_POWERUP_DEFAULT_TIME );    	

	if( skip )
		return; 
	
	if( isdefined(player.pyrotechnic_active) &amp;&amp; player.pyrotechnic_active == 1 )
		return;		
	
	player thread do_pyrotechnic();

}

function do_pyrotechnic()
{
	level.pyrotechnic_active = true;

    self notify( "pyrotechnic_done" );
    self endon( "disconnect" );
    self endon( "death" );
    self endon( "pyrotechnic_done" );

       
}

function knife_dmg(str_mod, str_hit_location, v_hit_origin, e_player, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel) 
{		

	if(level.pyrotechnic_active == 0)
	return 0;

	if(str_mod == "MOD_MELEE")
	{
		self thread fire_works_summon( e_player, w_weapon );
	}

	WAIT_SERVER_FRAME;
}

//HUD
function wait_til_timeout( player, hud )//player = self;
{
	while( hud.time &gt; 0 )
	{
		wait(1);
		hud.time--; 		
	}
	
	player notify( "pyrotechnic_done" );
	player remove_powerup_hud( "pyrotechnic_material_hud" );
	player.pyrotechnic_active = undefined;
	level.pyrotechnic_active = false;
	
	
}

function add_powerup_hud( powerup, timer )
{
	if ( !isDefined( self.powerup_hud ) )
		self.powerup_hud = [];
	
	if( isDefined( self.powerup_hud[powerup] ) )
	{
		self.powerup_hud[powerup].time = timer; 
		return true; // tells to skip because powerup is already active
				
	}
	
	self endon( "disconnect" );
	hud = NewClientHudElem( self );
	hud.powerup = powerup;
	hud.foreground = true;
	hud.hidewheninmenu = true;
	hud.alignX = "center";
	hud.alignY = "bottom";
	hud.horzAlign = "center";
	hud.vertAlign = "bottom";
	hud.x = hud.x;
	hud.y = hud.y - 50;
	hud.alpha = 1;
	hud SetShader( powerup , 64, 64 );
	hud scaleOverTime( .5, 32, 32 );
	hud.time = timer;
	hud thread harrybo21_blink_powerup_hud();
	thread wait_til_timeout( self, hud ); 
	
	self.powerup_hud[ powerup ] = hud;
	
	a_keys = GetArrayKeys( self.powerup_hud );
	for ( i = 0; i &lt; a_keys.size; i++ )
	 	self.powerup_hud[ a_keys[i] ] thread move_hud( .5, 0 - ( 24 * ( self.powerup_hud.size ) ) + ( i * 37.5 ) + 25, self.powerup_hud[ a_keys[i] ].y );
	
	return false;  // powerup is not already active
}




function move_hud( time, x, y )
{
	self moveOverTime( time );
	self.x = x;
	self.y = y;
}

function harrybo21_blink_powerup_hud()
{
	self endon( "delete" );
	self endon( "stop_fade" );
	while( isDefined( self ) )
	{
		if ( self.time &gt;= 20 )
		{
			self.alpha = 1; 
			wait .1;
			continue;
		}
		fade_time = 1;
		if ( self.time &lt; 10 )
			fade_time = .5;
		if ( self.time &lt; 5 )
			fade_time = .25;
			
		self fadeOverTime( fade_time );
		self.alpha = !self.alpha;
		
		wait( fade_time );
	}
}

function remove_powerup_hud( powerup )
{
	self.powerup_hud[ powerup ] destroy();
	self.powerup_hud[ powerup ] notify( "stop_fade" );
	self.powerup_hud[ powerup ] fadeOverTime( .2 );
	self.alpha = 0;
	wait .2;
	self.powerup_hud[ powerup ] delete();
	self.powerup_hud[ powerup ] = undefined;
	self.powerup_hud = array::remove_index( self.powerup_hud, self.powerup_hud[ powerup ], true );
	
	a_keys = GetArrayKeys( self.powerup_hud );
	for ( i = 0; i &lt; a_keys.size; i++ )
	 	self.powerup_hud[ a_keys[i] ] thread move_hud( .5, 0 - ( 24 * ( self.powerup_hud.size ) ) + ( i * 37.5 ) + 25, self.powerup_hud[ a_keys[i] ].y );
}

{% endraw %}{% endhighlight %}
</p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: mathfag<br /><blockquote><em>eDeK</em><br />mathfag in grab_pyrotechnic change  if( isdefined(player.pyrotechnic_active) )   to if( isdefined(player.pyrotechnic_active) &amp;&amp; player.pyrotechnic_active == 1 )   just incase.     The problem is that you set  level.pyrotechnic_active = false;   immediately after setting it to true. Actually you did it before but im guessing since the cpu has to process a bunch of code it gets set after. anyway Move level.pyrotechnic_active = false;   to the end of  function wait_til_timeout      I dont have words to describe my gratitude with you, seriously, thanks.   When i spawn in the game the &quot;Knife effect&quot; is active, i grab the powerup for the first time, the &quot;Knife effect&quot; still active, when the powerup is over, the &quot;Knife effect&quot; is over. (Everything goes back to normal)   I grab the powerup for the second time, the powerup is working perfect all the time. //POWERUP function __init__() { zm_powerups::register_powerup( &quot;pyrotechnic&quot;, &amp;grab_pyrotechnic ); if( ToLower( GetDvarString( &quot;g_gametype&quot; ) ) != &quot;pyrotechnic&quot; ) { zm_powerups::add_zombie_powerup( &quot;pyrotechnic&quot;, &quot;pyrotechnic_powerup_model&quot;, &quot;&quot;, &amp;zm_powerups::func_should_always_drop, POWERUP_ONLY_AFFECTS_GRABBER, !POWERUP_ANY_TEAM, !POWERUP_ZOMBIE_GRABBABLE ); zm_spawner::register_zombie_damage_callback( &amp;knife_dmg ); } } function grab_pyrotechnic( player ) { player PlayLocalSound(&quot;vox_pyrotechnic&quot;); skip = player add_powerup_hud( &quot;pyrotechnic_material_hud&quot;, N_POWERUP_DEFAULT_TIME ); if( skip ) return; if( isdefined(player.pyrotechnic_active) &amp;&amp; player.pyrotechnic_active == 1 ) return; player thread do_pyrotechnic(); } function do_pyrotechnic() { level.pyrotechnic_active = true; self notify( &quot;pyrotechnic_done&quot; ); self endon( &quot;disconnect&quot; ); self endon( &quot;death&quot; ); self endon( &quot;pyrotechnic_done&quot; ); } function knife_dmg(str_mod, str_hit_location, v_hit_origin, e_player, n_amount, w_weapon, direction_vec, tagName, modelName, partName, dFlags, inflictor, chargeLevel) { if(level.pyrotechnic_active == 0) return 0; if(str_mod == &quot;MOD_MELEE&quot;) { self thread fire_works_summon( e_player, w_weapon ); } WAIT_SERVER_FRAME; } //HUD function wait_til_timeout( player, hud )//player = self; { while( hud.time &gt; 0 ) { wait(1); hud.time--; } player notify( &quot;pyrotechnic_done&quot; ); player remove_powerup_hud( &quot;pyrotechnic_material_hud&quot; ); player.pyrotechnic_active = undefined; level.pyrotechnic_active = false; } function add_powerup_hud( powerup, timer ) { if ( !isDefined( self.powerup_hud ) ) self.powerup_hud = []; if( isDefined( self.powerup_hud[powerup] ) ) { self.powerup_hud[powerup].time = timer; return true; // tells to skip because powerup is already active } self endon( &quot;disconnect&quot; ); hud = NewClientHudElem( self ); hud.powerup = powerup; hud.foreground = true; hud.hidewheninmenu = true; hud.alignX = &quot;center&quot;; hud.alignY = &quot;bottom&quot;; hud.horzAlign = &quot;center&quot;; hud.vertAlign = &quot;bottom&quot;; hud.x = hud.x; hud.y = hud.y - 50; hud.alpha = 1; hud SetShader( powerup , 64, 64 ); hud scaleOverTime( .5, 32, 32 ); hud.time = timer; hud thread harrybo21_blink_powerup_hud(); thread wait_til_timeout( self, hud ); self.powerup_hud[ powerup ] = hud; a_keys = GetArrayKeys( self.powerup_hud ); for ( i = 0; i &lt; a_keys.size; i++ ) self.powerup_hud[ a_keys<em>] thread move_hud( .5, 0 - ( 24 * ( self.powerup_hud.size ) ) + ( i * 37.5 ) + 25, self.powerup_hud[ a_keys<em>].y ); return false; // powerup is not already active } function move_hud( time, x, y ) { self moveOverTime( time ); self.x = x; self.y = y; } function harrybo21_blink_powerup_hud() { self endon( &quot;delete&quot; ); self endon( &quot;stop_fade&quot; ); while( isDefined( self ) ) { if ( self.time &gt;= 20 ) { self.alpha = 1; wait .1; continue; } fade_time = 1; if ( self.time &lt; 10 ) fade_time = .5; if ( self.time &lt; 5 ) fade_time = .25; self fadeOverTime( fade_time ); self.alpha = !self.alpha; wait( fade_time ); } } function remove_powerup_hud( powerup ) { self.powerup_hud[ powerup ] destroy(); self.powerup_hud[ powerup ] notify( &quot;stop_fade&quot; ); self.powerup_hud[ powerup ] fadeOverTime( .2 ); self.alpha = 0; wait .2; self.powerup_hud[ powerup ] delete(); self.powerup_hud[ powerup ] = undefined; self.powerup_hud = array::remove_index( self.powerup_hud, self.powerup_hud[ powerup ], true ); a_keys = GetArrayKeys( self.powerup_hud ); for ( i = 0; i &lt; a_keys.size; i++ ) self.powerup_hud[ a_keys<em>] thread move_hud( .5, 0 - ( 24 * ( self.powerup_hud.size ) ) + ( i * 37.5 ) + 25, self.powerup_hud[ a_keys<em>].y ); }</em></em></em></em></blockquote><em><em><em><em>in function __init__ add <br />{% highlight php %}{% raw %}
level.pyrotechnic_active = 0;
{% endraw %}{% endhighlight %}
 <br />because if you do<br />{% highlight php %}{% raw %}
if(level.pyrotechnic_active == 0)
{% endraw %}{% endhighlight %}
 <br />when it&#39;s undefined it screws up<br /> <br />also the line<br />{% highlight php %}{% raw %}
if( ToLower( GetDvarString( "g_gametype" ) ) != "pyrotechnic" )
{% endraw %}{% endhighlight %}
is pointless</em></em></em></em></p>

---
<strong style="font-size: 1.4em;"><span style="text-decoration: underline;text-decoration-color: #34a7f9;"><span style="color:#34a7f9;">ModmeBot</span></span>:</strong>

<p>Reply By: eDeK<br />Yeah, the line is:<br />{% highlight php %}{% raw %}
if( ToLower( GetDvarString( "g_gametype" ) ) != "zcleansed" )
{% endraw %}{% endhighlight %}
My fault writing, work perfect now, thanks again, the unique thing i need buff is the score &quot;i cant do points, shooting or knifing, with the powerup active&quot; i dont know why, i need to do a callback to the player/zombie?</p>
